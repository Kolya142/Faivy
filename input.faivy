#import "Basics.faivy";

global :: main :: i32 -> () {
    local :: rule :: u8 = cast(110, u8);
    local :: state :: Ptr(u8) = cast(alloc(80), Ptr(u8));
    local :: back_state :: Ptr(u8) = cast(alloc(80), Ptr(u8));
    fill_bytes(cast(state, usize), cast(0, u8), 80);
    poke(state + cast(79, Ptr(u8)), cast(1, u8));
    local :: iteration :: usize = 0;
    local :: st :: usize = get_execution_ticks();
    iteration != 60 :: while {
	copy_bytes(cast(back_state, usize), cast(state, usize), 80);
	local :: i :: usize = 0;
	i != 80 :: while {
	    peek(state + cast(i, Ptr(u8))) == cast(1, u8) :: if {
		print_char(cast(35, u8));
	    }
	    else print_char(cast(32, u8));
	    i := i + 1;
	}
	print_char(cast(10, u8));
	i := 1;
	i != 79 :: while {
	    poke(back_state + cast(i, Ptr(u8)),
		 cast(
		     cast(rule, usize)
			 >>
			 cast(
			     (peek(state + cast(i-1, Ptr(u8)))<<cast(2, u8))+
				 (peek(state + cast(i, Ptr(u8)))<<cast(1, u8))+
				 (peek(state + cast(i+1, Ptr(u8)))),
			     usize
			 )
			 ,
		     u8) & cast(1, u8)
	    );
	    i := i + 1;
	}
	copy_bytes(cast(state, usize), cast(back_state, usize), 80);
	iteration := iteration + 1;
    }
    print_str("Frame ticks: ");
    print_usize(get_execution_ticks()-st);
    return(cast(0, i32));
}
