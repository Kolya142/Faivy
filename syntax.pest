WHITESPACE		= _{ " " | "\t" | "\r" | "\n" | COMMENT }
COMMENT			= _{ "//" ~ (!"\n" ~ ANY)* ~ ("\n" | EOI)
                | "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

program			=  { SOI ~ stmt* ~ EOI }
stmt			=  { if_stmt | for_stmt | while_stmt | variable | func | defer | struct_decl | import | asm | llvm | block | ( call ~ ";" ) | (set ~ ";") }

decl_extern_type	= { "extern" ~ identifier }
decl_global_type	= { "global" }
decl_local_type		= { "local" }
decl_forward_type	= { "forward" }
decl_const_type		= { "const" }
decl_type		= { decl_extern_type | decl_global_type | decl_local_type | decl_forward_type | decl_const_type }

args			= { ( field ~ ( "," ~ field )* )? }
expr_args		= { ( expr ~ ( "," ~ expr )* )? }

if_stmt			= { expr ~ "::" ~ "if" ~ block ~ ( "else" ~ stmt )? }
for_stmt		= { "for" ~ "::" ~ expr ~ "," ~ expr ~ "," ~ expr ~ block }
while_stmt		= { expr ~ "::" ~ "while" ~ block }
block			= { "{" ~ stmt* ~ "}" }

variable		= { ( decl_type ~ "::" ) ~ identifier ~ "::" ~ expr ~ ( "=" ~ expr )? ~ ";" }
set			= { expr ~ ":=" ~ expr }
func			= { ( decl_type ~ "::" ) ~ identifier ~ "::" ~ "(" ~ args ~ ")" ~ "->" ~ expr ~ ( block | ";" ) }
field			= { identifier ~ ":" ~ expr }
struct_decl		= { identifier ~ "::" ~ "struct" ~ "{" ~ ( field ~ ";" )* ~ "}" }
import			= { "#import" ~ string ~ ";" }
asm			= { "#asm" ~ string ~ ";" }
llvm			= { "#llvm" ~ string ~ ";" }
defer			= { "defer" ~ stmt }

binop			= { ( call | primary ) ~ binop_op ~ expr }
unaryop			= { unaryop_op ~ expr }
call			= { primary ~ "(" ~ expr_args ~ ")" }

expr_atom		= { binop | unaryop | call | primary }
expr			= { expr_atom }
primary			= { number | char_lit | string | identifier | "(" ~ expr ~ ")" }

binop_op		= @{ ">=" | "<=" | ">" | "<" | "==" | "!=" | ">>" | "<<" | "+" | "->" | "-" | "/" | "*" | "%" | "&" | "^" | "|" }
unaryop_op		= @{ "-&" | "+&" | "!" | "-" | "+" | "~" }
identifier		= @{ ("_" | ASCII_ALPHA) ~ ("_" | ASCII_ALPHANUMERIC)* }
number			= @{ ( "0x" ~ ( ASCII_DIGIT | "A" | "B" | "C" | "D" | "E" | "F" | "a" | "b" | "c" | "d" | "e" | "f" )+ | ( ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? )) }
string			= @{ "\"" ~ ( !("\"") ~ ANY )* ~ "\"" }
char_lit		= @{ "'" ~ ( !("'" ) ~ ANY ) ~ "'" }
